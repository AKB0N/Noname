import groovy.transform.Field
import java.text.SimpleDateFormat

def hasApp = project.plugins.hasPlugin("com.android.application")
if(!hasApp){
    return
}
@Field TAG = "COPYING:"
def targetBakPath = "build/apkBackup/${new SimpleDateFormat("yyyyMMddHHmm").format(new Date())}"


def deleteDirs(def dirs){
    file(dirs).listFiles().each{
        if(it.isDirectory()){
            deleteDirs(it.getPath())
        }else{
            it.delete()
        }
    }
    file(dirs).delete()
}

def copyTinkerBase (def targetDir) {
    def target = file("${targetDir}/tinkerBase")
    if(target.exists()){
        target.delete()
    }
    target.mkdirs()
    def targetPath = target.getPath()
    println(TAG + " copy to path: " + targetPath)
    def tinkerBakDir = file(tinkerSupport.autoBackupApkDir)
    println("$TAG find tinkerBakDir = $tinkerBakDir")
    if(tinkerBakDir == null || !tinkerBakDir.exists() || !tinkerBakDir.listFiles()[0].exists()){
        println("$TAG can not find tinkerBakDir")
        return
    }
    def tinkerBakPath = tinkerBakDir.listFiles()[0].getPath()
    println("$TAG tinkerBakPath = $tinkerBakPath")

    copy{ from tinkerBakPath
        into targetPath
        include "*.*"
    }
    println("$TAG delete  tinkerBakDir")
    deleteDirs(tinkerBakDir)
}

//拷贝Patch文件
def copyTinkerPatch(def targetDir){
    def target = file("$targetDir/patch")
    if(target.exists()){
        target.delete()
    }
    def targetPath = target.getPath()

    def patchDir = file("$buildDir/outputs/patch/release")
    if(patchDir == null || !patchDir.exists() || patchDir.listFiles().length <= 0){
        println("$TAG can not find patchDir files")
        return
    }
    println("$TAG copy patch files to="+targetDir)
    copy{ from patchDir.getPath()
        into targetPath
        include "*.*"
    }
    deleteDirs(patchDir.getPath())
}

def copyChannels(def targetDir){
    def target = file("$targetDir/channels")
    if(target.exists()){
        target.delete()
    }
    target.mkdirs()
    def targetPath = target.getPath()

    def channelsPath = channel.baseOutputDir.exists() ? channel.baseOutputDir.listFiles()[0].getPath() : null
    if(channelsPath == null){
        return
    }
    println("$TAG channelsPath = ${channelsPath}")

    copy{ from channelsPath
        into targetPath
        include "*.*"
    }
}

project.afterEvaluate{

    def releaseTask = "assembleRelease"
    def channelTask = "channelRelease"
    def tinkerPatchTask = "buildTinkerPatchRelease"

    def hasReleaseTask = tasks.findByName(releaseTask) != null;

    if(!hasReleaseTask){
        return
    }
    //判断当前执行的任务
    def hasChannelTask = false
    def hasTinkerPatchTask = false
    gradle.startParameter.getTaskNames().each { taskName ->
        if (channelTask == taskName){
            hasChannelTask = true
        }else if(tinkerPatchTask == taskName){
            hasTinkerPatchTask = true
        }
    }

    println("$TAG hasTinkerPatchTask=$hasTinkerPatchTask, hasChannelTask=$hasChannelTask")

    if(hasChannelTask){
        targetBakPath = targetBakPath + "-channels"
    }else if(hasTinkerPatchTask){
        targetBakPath = targetBakPath + "-patch"
    }else{
        return
    }

    if(hasReleaseTask){
        tasks.getByName(releaseTask) {
            it.doLast {
                copyTinkerBase(targetBakPath)
            }
        }
    }

    if(hasTinkerPatchTask){
        tasks.getByName(tinkerPatchTask) {
            it.doLast {
                copyTinkerPatch(targetBakPath)
            }
        }
    }

    if(hasChannelTask){
        tasks.getByName(channelTask) {
            it.doLast {
                copyChannels(targetBakPath)
            }
        }
    }
}